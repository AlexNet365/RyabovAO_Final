
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)   
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);    
	
	// ++ ВКМ Добавление комнады "Заполнить"
	КомандаЗаполнить = Команды.Добавить("Заполнить");
	КомандаЗаполнить.Заголовок = "Заполнить"; 
	КомандаЗаполнить.Подсказка = "Заполнить данные по абонентскому договору";
	КомандаЗаполнить.Действие = "Заполнить";
	КнопкаКоманды = Элементы.Вставить("Заполнить", Тип("КнопкаФормы"), КоманднаяПанель);
	КнопкаКоманды.ИмяКоманды = "Заполнить";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	// -- ВКМ
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	// ++ ВКМ Уточнение команды "Заполнить"
	Если ЗначениеЗаполнено(Объект.Договор) Тогда
		ЗаполнитьНаСервере();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Договор не заполнен";
		Сообщение.Сообщить();
	КонецЕсли; 
	// -- ВКМ
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	// ++ ВКМ Команда "Заполнить"
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Договор,
		"ВидДоговора, ВКМ_НачалоДействияДоговора, ВКМ_КонецДействияДоговора");
	Если Данные.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскаяПлата Тогда
		Если Данные.ВКМ_НачалоДействияДоговора < Объект.Дата И Данные.ВКМ_КонецДействияДоговора > Объект.Дата Тогда
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			ДокументОбъект.ВыполнитьАвтозаполнение();
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Действие договора истекло");
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьПользователю("Выберете абонентский договор");
	КонецЕсли;
	// -- ВКМ
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
