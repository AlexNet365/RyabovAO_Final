
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//Акт об оказанных услугах 
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт'");
	КомандаПечати.Порядок = 5;      
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Акт");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.АР_Доставка.ПФ_MXL_Акт";
	КонецЕсли;   
	
КонецПроцедуры    

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)  
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт"); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Номер КАК Номер,
		|	РеализацияТоваровУслуг.Дата КАК Дата,
		|	РеализацияТоваровУслуг.Товары.(
		|		Номенклатура КАК Номенклатура,
		|		Количество КАК Количество,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма
		|	) КАК Товары,
		|	РеализацияТоваровУслуг.Услуги.(
		|		Номенклатура КАК Номенклатура,
		|		Количество КАК Количество,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма
		|	) КАК Услуги,
		|	РеализацияТоваровУслуг.Организация КАК Организация,
		|	РеализацияТоваровУслуг.Контрагент КАК Контрагент
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();         
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл

		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;  
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка"); 
		ОбластьШапка.Параметры.Дата = Формат(Выборка.Дата, "ДЛФ=DD");
		ОбластьШапка.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер);
		ОбластьШапка.Параметры.Организация = Выборка.Организация;   
		ОбластьШапка.Параметры.Контрагент = Выборка.Контрагент; 
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТЧ");  
		ТабличныйДокумент.Вывести(ОбластьШапкаТЧ); 
		
		ОбластьТЧ = Макет.ПолучитьОбласть("ТЧ");   
		
		Номер = 0;
		СуммаИтог = 0;
		
		ВыборкаТЧТовары = Выборка.Товары.Выбрать();
		
		Пока ВыборкаТЧТовары.Следующий() Цикл 
			Номер = Номер + 1;
			СуммаИтог = СуммаИтог + ВыборкаТЧТовары.Сумма; 
			ОбластьТЧ.Параметры.Заполнить(ВыборкаТЧТовары);  
			ОбластьТЧ.Параметры.Заполнить(Новый Структура("Номер", Номер)); 
			ТабличныйДокумент.Вывести(ОбластьТЧ);
		КонецЦикла;    
		
		ВыборкаТЧУслуги = Выборка.Услуги.Выбрать();
		
		Пока ВыборкаТЧУслуги.Следующий() Цикл     
			Номер = Номер + 1;
			СуммаИтог = СуммаИтог + ВыборкаТЧУслуги.Сумма;
			ОбластьТЧ.Параметры.Заполнить(ВыборкаТЧУслуги); 
			ОбластьТЧ.Параметры.Заполнить(Новый Структура("Номер", Номер));
			ТабличныйДокумент.Вывести(ОбластьТЧ);
		КонецЦикла; 
		
		ОбластьИтогТЧ = Макет.ПолучитьОбласть("ИтогТЧ"); 
		ОбластьИтогТЧ.Параметры.ИтоговаяСумма = СуммаИтог;
		ОбластьИтогТЧ.Параметры.ИтоговаяСуммаПрописью = ЧислоПрописью(СуммаИтог,"Л=ru_RU; ДП=Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж" );;
		ТабличныйДокумент.Вывести(ОбластьИтогТЧ);
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); 
		ТабличныйДокумент.Вывести(ОбластьПодвал);         
		
		ПервыйДокумент = Ложь;
		
	КонецЦикла;   
		
	Возврат ТабличныйДокумент;
	
КонецФункции






#КонецОбласти

#КонецЕсли